buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'

import net.minecraftforge.gradle.common.tasks.SignJar

import java.nio.file.Files

version = "1.18-v1"
group = "lain.mods.skins"
archivesBaseName = "OfflineSkins"

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))
minecraft {
    mappings channel: 'official', version: '1.18'
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

dependencies {
    minecraft 'net.minecraftforge:forge:1.18-38.0.2'
}

processResources {
    from 'LICENSE.txt'
}

jar {
    manifest {
        attributes(["Specification-Title"     : "OfflineSkins",
                    "Specification-Vendor"    : "zlainsama",
                    "Specification-Version"   : "1",
                    "Implementation-Title"    : project.name,
                    "Implementation-Version"  : "${version}",
                    "Implementation-Vendor"   : "zlainsama",
                    "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")],)
    }
}

jar.finalizedBy('reobfJar')

task signJar(type: SignJar, dependsOn: jar) {
    onlyIf {
        System.getenv('SIGNJAR_KEYSTORE') != null
    }

    if (System.getenv('SIGNJAR_KEYSTORE') != null) {
        keyStore = Files.createTempFile(getTemporaryDir().toPath(), 'keystore-', '.jks').toString()
        file(keyStore).withOutputStream { output ->
            output.write(Base64.getDecoder().decode(System.getenv('SIGNJAR_KEYSTORE')))
        }
        alias = System.getenv('SIGNJAR_ALIAS')
        storePass = System.getenv('SIGNJAR_STOREPASS')
        keyPass = System.getenv('SIGNJAR_KEYPASS')
        inputFile = jar.archivePath
        outputFile = jar.archivePath
    }

    doLast {
        delete keyStore
    }
}

build.dependsOn signJar
