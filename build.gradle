buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'

import net.minecraftforge.gradle.common.task.SignJar

version = "1.16.1-v1"
group= "lain.mods.skins"
archivesBaseName = "OfflineSkins"

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

minecraft {
    mappings channel: 'snapshot', version: '20200514-1.16'
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.16.1-32.0.47'
}

processResources {
    from 'LICENSE.txt'
}

jar {
    manifest {
        attributes(["Specification-Title": "OfflineSkins",
                    "Specification-Vendor": "zlainsama",
                    "Specification-Version": "1",
                    "Implementation-Title": project.name,
                    "Implementation-Version": "${version}",
                    "Implementation-Vendor" :"zlainsama",
                    "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")],)
    }
}

task signJar(type: SignJar, dependsOn: jar) {
    onlyIf {
        project.hasProperty('mcModsStore')
    }

    if (project.hasProperty('mcModsStore')) {
        keyStore = project.mcModsStore
        alias = project.mcModsAlias
        storePass = project.mcModsStorePass
        keyPass = project.mcModsKeyPass
        inputFile = jar.archivePath
        outputFile = jar.archivePath
    }
}

build.dependsOn signJar